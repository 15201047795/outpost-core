#!/usr/bin/env python

import os

# root folder of the outpost library
rootpath    = '../../../..'
modulepath  = os.path.join(rootpath, 'modules')
buildfolder = os.path.join(rootpath, 'build')

envGlobal = Environment(toolpath=[rootpath + '/scons-tools'],
                        tools=['compiler_sparc_rtems_gcc', 'settings_buildpath', 'utils_buildformat', 'utils_buildsize'],
                        DEVICE_SIZE={
                            'name' : 'Nexys 3 - LEON3',
                            'flash': 16777216,
                            'ram'  : 16777216
                        },
                        OS='rtems',
                        BOARD='nexys3',
                        ENV=os.environ)

envGlobal['CCFLAGS_other'] = ['-qleon3std']

envGlobal['BASEPATH']  = os.path.abspath('.')
envGlobal['BUILDPATH'] = os.path.abspath(os.path.join(buildfolder, 'rtos/test/rtems'))

envGlobal.Append(CPPPATH=[
    os.path.abspath(os.path.join(rootpath, 'modules/spp/default')),
    os.path.abspath(os.path.join(rootpath, 'modules/pus/default')),
    os.path.abspath(os.path.join(rootpath, 'modules/log/default')),
])

envGlobal.SConscript(os.path.join(rootpath, 'modules/SConscript.library'), exports='envGlobal')

# Build outpost-hw
# envGlobal.SConscript(os.path.join(rootpath, 'modules/rtos/ext/outpost-hw/src/SConscript'), exports='envGlobal')

env = envGlobal.Clone()

env.Append(CPPPATH=[
    '.',
    '../reference'
])

env.AppendUnique(LIBS=[
	'outpost_hal',
    'outpost_log',
    'outpost_pus',
    'outpost_spp',
    'outpost_rtos',
    'outpost_smpc',
    'outpost_time',
    'outpost_utils',
])
env.Append(LIBPATH=['$BUILDPATH/lib'])

files  = env.Object(env.FilteredGlob(['*.cpp', '../reference/*.cpp'], ['main.cpp']))

env = env.Clone()
env.RemoveFromList('CXXFLAGS_warning', '-Wold-style-cast')

files.append(env.Object('main.cpp'))

program = env.Program('rtems.elf', files)

envGlobal.Alias('build', program)
envGlobal.Alias('install', env.Install('bin/', program))
envGlobal.Alias('size', env.Size(program))

envGlobal.Default('build')

