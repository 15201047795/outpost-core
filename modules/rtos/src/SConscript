#!/usr/bin/env python

import os

Import('envGlobal')

envGlobal.Append(CPPPATH=[os.path.abspath('.')])

env = envGlobal.Clone()

files  = env.Glob('cobc/rtos/*.cpp')
files += env.Glob('cobc/hal/*.cpp')

if env['OS'] == 'none':
	rtos = 'COBC_RTOS_NONE'
	
	files += env.Glob('cobc/rtos/none/*.cpp')
elif env['OS'] == 'rtems':
	rtos = 'COBC_RTOS_RTEMS'
	
	files += env.Glob('cobc/rtos/rtems/*.cpp')
	
	# Leon 3
	if env['ARCHITECTURE'] == 'leon3':
		# Remove warning because the spacewirelight driver uses void pointer
		# casts which ignore qualifiers like volatile.
		env.RemoveFromList('CCFLAGS_warning', '-Wcast-qual')
		
		# spacewirelight can not be compiled as C99 (with or without pedantic)
		env['CFLAGS_language'] = '-std=gnu99'
		
		files += env.Glob('../ext/rtems-leon3/spwl/*.c')
		files += env.Glob('../ext/rtems-leon3/apbuart/*.c')
		files += env.Glob('../ext/rtems-leon3/grspw/*.c')
		
		files += env.Glob('cobc/hal/rtems-leon3/*.cpp')
		
		# TODO only when compiling for the nexys3 board!
		files += env.Glob('cobc/hal/nexys3/*.cpp')
	
	# as RTEMS is C it uses C-Style casts. Therefore this warning has to
	# disabled for the RTEMS wrapper files.
	env.RemoveFromList('CXXFLAGS_warning', '-Wold-style-cast')
	
elif env['OS'] == 'freertos':
	rtos = 'COBC_RTOS_FREERTOS'
	
	files += env.Glob('cobc/rtos/freertos/*.cpp')
	
	# FreeRTOS uses C-Style casts. Therefore this warning has to
	# disabled for the FreeRTOS wrapper files.
	env.RemoveFromList('CXXFLAGS_warning', '-Wold-style-cast')
elif env['OS'] == 'posix':
	rtos = 'COBC_RTOS_POSIX'
	files += env.Glob('cobc/rtos/posix/*.cpp')
	
	envGlobal.Append(LIBS=['rt', 'pthread'])
else:
	print "Error: Environment variable 'OS' not defined. Set it to " +\
		  "none|rtems|freertos|posix to define the used operating system!"
	Exit(1)

env.Append(CPPDEFINES={'COBC_RTOS':rtos})
envGlobal.Append(CPPDEFINES={'COBC_RTOS':rtos})

objects = []
for file in files:
	objects.append(env.Object(file))
envGlobal.Append(objects={'rtos': objects})

library = env.StaticLibrary('cobc_rtos', objects)
envGlobal.Install('$BUILDPATH/lib', library)

