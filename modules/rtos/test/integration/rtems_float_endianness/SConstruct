#!/usr/bin/env python

import os

rootpath    = '../../../../..'
modulepath  = os.path.join(rootpath, 'modules')
buildfolder = os.path.join(modulepath, '../build')

envGlobal = Environment(toolpath=['../../../../../conf/scons/site_tools'],
                        tools=['rtems-leon3', 'cobc', 'buildpath', 'buildformat', 'buildsize'],
                        DEVICE_SIZE={
                            'name' : 'Nexys 3 - LEON3',
                            'flash': 16777216,
                            'ram'  : 16777216
                        },
                        ENV=os.environ)

envGlobal['BASEPATH']  = os.path.abspath('.')
envGlobal['BUILDPATH'] = os.path.abspath(os.path.join(buildfolder, 'rtos/test/integration/rtems_float_endianness'))

envGlobal.SConscript([modulepath + '/utils/src/SConscript',
                      modulepath + '/time/src/SConscript',
                      '../../../src/SConscript'],
                     exports='envGlobal')

env = envGlobal.Clone()

env['CFLAGS_language'] = ['-std=gnu99']

env.Append(CPPPATH=[
    '.'
])
env.Append(LIBS=['cobc_rtos', 'cobc_time'])
env.Append(LIBPATH=['$BUILDPATH/lib'])

files  = env.Glob('*.c')
files += env.Glob('*.cpp')

program = env.Program('rtems_float_endianness', files)

envGlobal.Alias('build', program)
envGlobal.Alias('install', env.Install(program))
envGlobal.Alias('size', env.Size(program))

envGlobal.Default(['build', 'size'])

