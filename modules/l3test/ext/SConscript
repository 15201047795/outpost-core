#!/usr/bin/env python
# -*- coding: utf-8 -*-
#
# Copyright (c) 2014-2015, 2017, German Aerospace Center (DLR)
#
# This file is part of the development version of OUTPOST.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.
#
# Authors:
# - 2014-2015, 2017, Fabian Greif (DLR RY-AVS)

import os

Import('envGlobal')

envGlobal.Append(CPPPATH=[os.path.abspath('lua-5.3.4/src')])
envGlobal.Append(LIBS=['dl'])

env = envGlobal.Clone()

files  = env.Glob('lua-5.3.4/src/*.c')
to_remove = []
for file in files:
    filename = str(file)
    if filename.endswith('lua.c') or filename.endswith('luac.c'):
        to_remove.append(file)

for file in to_remove:
    files.remove(file)

# Remove warnings triggered by Lua
env.RemoveFromList('CCFLAGS_warning', ['-Wcast-align', '-Wcast-qual'])
env.RemoveFromList('CFLAGS_language', '-pedantic')

env.Append(CPPDEFINES=['LUA_USE_LINUX'])

objects = []
for file in files:
	objects.append(env.Object(file))
envGlobal.Append(objects={'l3test-lua': objects})

library = env.StaticLibrary('l3test_lua', objects)
envGlobal.Install('$BUILDPATH/lib', library)
