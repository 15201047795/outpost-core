#!/usr/bin/env python
# -*- coding: utf-8 -*-

import os

rootpath = '../../'
outpostpath = rootpath + '../../'
modulepath = outpostpath + 'modules/'

envGlobal = Environment(toolpath=[outpostpath + 'scons-tools'],
			            tools=[
							'utils_buildformat',
							'compiler_hosted_gcc',
							'settings_buildpath',
							'utils_common'
						],
				        ENV=os.environ)

envGlobal['BASEPATH'] = os.path.abspath('.')
envGlobal['BUILDPATH'] = os.path.abspath(outpostpath + 'build/l3test/lua')

# Binding code between Lua (written in C) and C++ has to use some C casts
envGlobal.RemoveFromList('CXXFLAGS_warning', ['-Wold-style-cast'])

# -----------------------------------------------------------------------------
# Build bitstream module

env = envGlobal.Clone()

bitstream_files = env.Glob('bitstream/*.cpp')
bitstream_library = env.SharedLibrary(outpostpath + 'bin/lua/bitstream', bitstream_files, SHLIBPREFIX='')

# -----------------------------------------------------------------------------
# Build crc16 module

env = envGlobal.Clone()

# Overwrite the global enviroment to compile the outpost module with the
# PIC settings. This is required to be able to link everything into a
# shared library
envGlobalTemp = envGlobal
envGlobal = env

env.Append(CCFLAGS=['-fPIC'])

env.SConscript([
        os.path.join(modulepath, 'utils/src/SConscript'),
    ],
    exports='envGlobal')

env.Append(LIBS=['outpost_utils'])
env.Append(LIBPATH=['/usr/lib', '$BUILDPATH/lib'])

crc16_files = env.Glob('crc16/*.cpp')
crc16_library = env.SharedLibrary(outpostpath + 'bin/lua/crc16', crc16_files, SHLIBPREFIX='')

# Build COBS module
cobs_files = env.Glob('cobs/*.cpp')
cobs_library = env.SharedLibrary(outpostpath + 'bin/lua/cobs', cobs_files, SHLIBPREFIX='')

# restore global environment
envGlobal = envGlobalTemp

# -----------------------------------------------------------------------------
# Build l3test module

env = envGlobal.Clone()

env['CXXFLAGS_language'] = ['-std=c++0x']
env.Append(CPPPATH=[rootpath + 'src'])
env.Append(CPPPATH=['.', '..'])

l3test_files = env.Glob('l3test/*.cpp')
l3test_files += env.Glob(rootpath + 'src/l3test/*/*.cpp')

l3test_library = env.SharedLibrary(outpostpath + 'bin/lua/l3test', l3test_files, SHLIBPREFIX='')

# -----------------------------------------------------------------------------
# Global
envGlobal.Default([bitstream_library, crc16_library, l3test_library, cobs_library])
