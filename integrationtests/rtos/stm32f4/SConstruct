#!/usr/bin/env python

import os

rootpath    = '../../../../..'
modulepath  = os.path.join(rootpath, 'modules')
buildfolder = os.path.join(modulepath, '../build')

envGlobal = Environment(toolpath=['../../../../../conf/scons/site_tools'],
                        tools=['arm-none-eabi', 'buildpath', 'buildformat', 'cobc'],
                        OS='freertos',
                        ENV=os.environ)

envGlobal['BASEPATH']  = os.path.abspath('.')
envGlobal['BUILDPATH'] = os.path.abspath(os.path.join(buildfolder, 'rtos/test/integration/stm32f4'))

envGlobal.Append(CPPPATH=[
    os.path.abspath(modulepath + '/rtos/ext')
])

envGlobal.SConscript([modulepath + '/utils/src/SConscript',
                      modulepath + '/time/src/SConscript',
                      '../../../src/SConscript'],
                     exports='envGlobal')

env = envGlobal.Clone()
env.Append(CPPPATH=[
    '.'
])

env.Append(LIBS=['cobc_rtos', 'cobc_time'])
env.Append(LIBPATH=['$BUILDPATH/lib'])

files  = env.Glob('*.cpp')

envGlobal.Alias('build', env.Program('test-stm32f4', files))

